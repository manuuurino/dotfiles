# Personal Zsh configuration file. It is strongly recommended to keep all
# shell customization and configuration (including exported environment
# variables such as PATH) in this file or in files sourced from it.
#
# Documentation: https://github.com/romkatv/zsh4humans/blob/v5/README.md.

# Periodic auto-update on Zsh startup: 'ask' or 'no'.
# You can manually run `z4h update` to update everything.
zstyle ':z4h:' auto-update      'no'
# Ask whether to auto-update this often; has no effect if auto-update is 'no'.
zstyle ':z4h:' auto-update-days '28'

# Keyboard type: 'mac' or 'pc'.
zstyle ':z4h:bindkey' keyboard  'pc'

# Start tmux if not already in tmux.
# zstyle ':z4h:' start-tmux command tmux -u new -A -D -t z4h
zstyle ':z4h:' start-tmux no

# Whether to move prompt to the bottom when zsh starts and on Ctrl+L.
zstyle ':z4h:' prompt-at-bottom 'no'

# Mark up shell's output with semantic information.
zstyle ':z4h:' term-shell-integration 'yes'

# Right-arrow key accepts one character ('partial-accept') from
# command autosuggestions or the whole thing ('accept')?
zstyle ':z4h:autosuggestions' forward-char 'accept'

# Recursively traverse directories when TAB-completing files.
zstyle ':z4h:fzf-complete' recurse-dirs 'yes'

# Enable direnv to automatically source .envrc files.
zstyle ':z4h:direnv'         enable 'yes'
# Show "loading" and "unloading" notifications from direnv.
zstyle ':z4h:direnv:success' notify 'yes'

# Completions
## SSH
zstyle ':completion:*:ssh:argument-1:'       tag-order  hosts users
zstyle ':completion:*:scp:argument-rest:'    tag-order  hosts files users
zstyle ':completion:*:(ssh|scp|rdp):*:hosts' hosts

## Kill
zstyle ':completion:*:*:*:*:processes' command 'ps -u $USER -o pid,user,comm -w'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;36=0=01'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*' insert-ids single

# Don't complete uninteresting users...
zstyle ':completion:*:*:*:users' ignored-patterns \
	adm amanda apache avahi beaglidx bin cacti canna clamav daemon \
	dbus distcache dovecot fax ftp games gdm gkrellmd gopher \
	hacluster haldaemon halt hsqldb ident junkbust ldap lp mail \
	mailman mailnull mldonkey mysql nagios \
	named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
	operator pcap postfix postgres privoxy pulse pvm quagga radvd \
	rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs '_*'

# Enable ('yes') or disable ('no') automatic teleportation of z4h over
# SSH when connecting to these hosts.
zstyle ':z4h:ssh:*.sv'   enable 'yes'
zstyle ':z4h:ssh:*.dev'  enable 'yes'
# The default value if none of the overrides above match the hostname.
zstyle ':z4h:ssh:*'                   enable 'no'

# Send these files over to the remote host when connecting over SSH to the
# enabled hosts.
zstyle ':z4h:ssh:*' send-extra-files '~/.p10k.zsh' '~/.env.zsh'

# Clone additional Git repositories from GitHub.
z4h install ohmyzsh/ohmyzsh || return
z4h install MichaelAquilina/zsh-you-should-use || return
z4h install momo-lab/zsh-replace-multiple-dots || return
z4h install redxtech/zsh-show-path || return
z4h install grigorii-zander/zsh-npm-scripts-autocomplete || return

# Install or update core components (fzf, zsh-autosuggestions, etc.) and
# initialize Zsh. After this point console I/O is unavailable until Zsh
# is fully initialized. Everything that requires user interaction or can
# perform network I/O must be done above. Everything else is best done below.
z4h init || return

# Extend PATH.
path=(~/.local/bin ~/bin $path ~/.emacs.d/bin)

# Export environment variables.
export GPG_TTY=$TTY

# Source additional local files if they exist.
z4h source ~/.env.zsh

# Options
TREE_IGNORE="cache|log|logs|node_modules|vendor|.git"

# more configuration see https://github.com/ohmyzsh/ohmyzsh/blob/master/plugins/magic-enter/magic-enter.plugin.zsh
MAGIC_ENTER_OTHER_COMMAND="l"

# Use additional Git repositories pulled in with `z4h install`.
z4h source ohmyzsh/ohmyzsh/lib/clipboard.zsh
z4h load   ohmyzsh/ohmyzsh/plugins/extract
z4h load   ohmyzsh/ohmyzsh/plugins/universalarchive
z4h load   ohmyzsh/ohmyzsh/plugins/jsontools
z4h load   ohmyzsh/ohmyzsh/plugins/magic-enter
z4h source MichaelAquilina/zsh-you-should-use/you-should-use.plugin.zsh
z4h source momo-lab/zsh-replace-multiple-dots/replace-multiple-dots.plugin.zsh
z4h load   redxtech/zsh-show-path
z4h load   grigorii-zander/zsh-npm-scripts-autocomplete

# Define key bindings.
z4h bindkey z4h-backward-kill-word  Ctrl+Backspace     Ctrl+H
z4h bindkey z4h-backward-kill-zword Ctrl+Alt+Backspace

z4h bindkey undo Ctrl+/ Shift+Tab  # undo the last command line change
z4h bindkey redo Alt+/             # redo the last undone command line change

z4h bindkey z4h-cd-back    Alt+Left   # cd into the previous directory
z4h bindkey z4h-cd-forward Alt+Right  # cd into the next directory
z4h bindkey z4h-cd-up      Alt+Up     # cd into the parent directory
z4h bindkey z4h-cd-down    Alt+Down   # cd into a child directory

# Autoload functions.
autoload -Uz zmv

[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh" --no-use

# Define functions and completions.
function _yay_fzf()
{
	yay -Sl \
		| awk '{print $2 " / " $1 " " $4}' \
		| fzf --multi --preview 'cat <(yay -Si {1}) <(yay -Fl {1} | awk "{print $2}")' \
		| awk '{print $1}' \
		| xargs -rot yay $1
}

function _fallback_program
{
	if command -v "$(echo "$1" | awk '{print $1};')" >/dev/null 2>&1; then
		echo "$1"
	else
		echo "$2"
	fi
}

function md() { [[ $# == 1 ]] && mkdir -p -- "$1" && cd -- "$1" }
compdef _directories md

function apply-zshrc-to-user()
{
	local from="$HOME"
	local prefix=""
	local target_files="$from/{.zsh{rc,env},.p10k.zsh}"

	if (( $# == 0 )); then
		apply-zshrc-to-user -h
		return
	fi;

	while [ $# -gt 0 ]; do
		opt="$1";
		shift;

		case "$opt" in
			"-h" | "--help" )
				cat >&2 <<'EOF'
Usage: apply-zshrc-to-user [-option] [user]
Options:
	-h, --help	show list of command-line options.
	-s, --sudo	runs su with sudo.
	-f, --files	default set to "$HOME/{.zsh{rc,env},.p10k.zsh}"
EOF
				return
				;;
			"-s" | "--sudo" )
				prefix="sudo"
				;;
			"-f" | "--files" )
				target_files="$1";
				shift
				;;
		esac
	done

	"$prefix" su "$1" -s /bin/bash -c "cd ~ && cp $target_files ."
}
compdef _users apply-zshrc-to-user

# fuzzy rg
function frg() {
	rg --line-number \
		--column \
		--no-heading \
		--color=always \
		"$@" . 2> /dev/null \
		| fzf --ansi
}

function _toggle_suffix_file_name()
{
	if [[ -e "$1" ]]; then
        mv --interactive --verbose "$1"{,"$2"}
    elif [[ -e "$1$2" ]]; then
        mv --interactive --verbose "$1"{"$2",}
    fi
}

backup() {
    _toggle_suffix_file_name $1 ".backup"
}
compdef _files backup

disable() {
    _toggle_suffix_file_name $1 ".disable"
}
compdef _files disable

# Define named directories: ~w <=> Windows home directory on WSL.
[[ -z $z4h_win_home ]] || hash -d w=$z4h_win_home

# Define aliases.
alias reload="exec zsh"

alias yf=_yay_fzf
alias pum='sudo reflector -c de --verbose -f 8 -p https --save /etc/pacman.d/mirrorlist'
alias mixer="ncpamixer"
alias xcb="xclip -sel c"

alias diff="diff --color=auto --new-file --recursive --ignore-all-space --unified --text"

alias cp='cp --interactive --verbose'
alias mv='mv --interactive --verbose'
alias rm='rm --interactive --verbose'

alias rcp='rsync --verbose --partial --progress --human-readable --perms --owner --group'
alias rmv='rcp --remove-source-files'

alias rmdir='rmdir --verbose'
alias ln='ln --verbose'
alias chmod="chmod --changes --verbose"
alias chown="chown --changes --verbose"
alias mkdir="mkdir --verbose"

alias grep='grep --colour=auto'
alias egrep='egrep --colour=auto'

alias md="mkdir -p"
alias rd="rmdir"

alias compress="ua"

# Add flags to existing aliases.
{
	local LS_CMD="ls"
	local LS_OPTS=("--group-directories-first" "--classify")
	local LS_LONG_OPTS=("$LS_OPTS" "-l" "--human-readable" "--inode")
	local LS_LONG_VERBOSE_OPTS=("$LS_VERBOSE_OPTS")

	local TREE_CMD="tree"
	local TREE_OPTS=("-I $TREE_IGNORE")
	local TREE_LONG_OPTS=()
	local TREE_LONG_VERBOSE_OPTS=()
	
	if command -v exa >/dev/null 2>&1; then
		LS_CMD="exa"
		LS_OPTS=("--group-directories-first" "--classify" "--icons")
		LS_LONG_OPTS=("--group" "--long" "--header" "--binary" "--octal-permissions" "--git" "--extended" "--color-scale")
		LS_LONG_VERBOSE_OPTS=("--created" "--modified" "--accessed" "--links" "--inode" "--blocks")

		TREE_CMD="exa --tree"
		TREE_OPTS=("-I '$TREE_IGNORE'" "$LS_OPTS")
		TREE_LONG_OPTS=("$LS_LONG_OPTS")
		TREE_LONG_VERBOSE_OPTS=("$LS_LONG_VERBOSE_OPTS")
	fi

	alias ls="$LS_CMD $LS_OPTS"
	alias l='ls'
	alias la='ls --all'
	alias ll="ls $LS_OPTS $LS_LONG_OPTS"
	alias lla="ll --all"
	alias llx="ls $LS_OPTS $LS_LONG_OPTS $LS_LONG_VERBOSE_OPTS"

	alias tree="$TREE_CMD $TREE_OPTS"
	alias ta="tree --all"
	alias tl="tree $TREE_OPTS $TREE_LONG_OPTS"
	alias tla="tl --all"
	alias tlx="tla $TREE_OPTS $TREE_LONG_OPTS $TREE_LONG_VERBOSE_OPTS"
}


# Set shell options: http://zsh.sourceforge.net/Doc/Release/Options.html.
setopt glob_dots     # no special treatment for file names with a leading dot
setopt no_auto_menu  # require an extra TAB press to open the completion menu
